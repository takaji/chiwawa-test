group 'jp.co.dreamarts.chiwawa'
version '1.0-SNAPSHOT'


buildscript {
    ext {
        springBootVersion = '1.4.0.RELEASE'
        kotlin_version = '1.0.3'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    jcenter()
    mavenCentral()
}


apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: "spring-boot"
apply plugin: "idea"

sourceCompatibility = 1.8
targetCompatibility = 1.8

def defaultEncoding = 'UTF-8'

compileJava.options.encoding = defaultEncoding
compileTestJava.options.encoding = defaultEncoding

configurations {
    provided
    compile.exclude module: "spring-boot-starter-tomcat"
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

bootRun {
    addResources = true
}

springBoot {
    backupSource = false
}

bootRepackage {
    mainClass = 'jp.co.dreamarts.chiwawa.Application'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-jetty')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    //compile('org.springframework.boot:spring-boot-starter-websocket')
    //compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    //compile('org.springframework.security.oauth:spring-security-oauth2')
    //compile('org.springframework.session:spring-session:1.2.1.RELEASE')
    compile('org.postgresql:postgresql:9.4.1209.jre7')
    compile('ch.qos.logback:logback-core:1.1.5')
    compile('ch.qos.logback:logback-classic:1.1.5')
    compile('org.codehaus.janino:janino:2.7.8')
    compile('org.flywaydb:flyway-core')

    provided('org.springframework.boot:spring-boot-devtools')
    provided('org.projectlombok:lombok:1.16.10')

    testCompile("junit:junit:4.12")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("com.h2database:h2:1.4.192")
}
